#include <WiFiNINA.h>
#include <ArduinoHttpClient.h>
#include <ArduinoJson.h>

const char* ssid = "KatherineiPhone";
const char* password = "Katherine@123";

const char* server = "172.20.10.2";  // Your laptop's IP
const int port = 8080;

WiFiClient wifi;
HttpClient client = HttpClient(wifi, server, port);

void sendMood(int mood) {
  StaticJsonDocument<200> jsonDoc;
  jsonDoc["device_id"] = 1;  // deviceID must be INT (matches Go backend)
  jsonDoc["mood"] = mood;

  String jsonData;
  serializeJson(jsonDoc, jsonData);

  Serial.print("Sending: ");
  Serial.println(jsonData);

  client.beginRequest();
  client.post("/addMood");   // must match your Go route
  client.sendHeader("Content-Type", "application/json");
  client.sendHeader("Content-Length", jsonData.length());
  client.endRequest();
  client.print(jsonData);

  int status = client.responseStatusCode();
  String response = client.responseBody();

  Serial.print("Status: ");
  Serial.println(status);
  Serial.print("Response: ");
  Serial.println(response);
}

const int pinButtonA = 8;
const int pinButtonB = 9;
const int pinButtonC = 10;

const int pinLedA = 12;
const int pinLedB = 7;
const int pinLedC = 6;

void setup() {
  Serial.begin(9600);
  while (!Serial);

  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.println("Connecting...");
  }
  Serial.println("WiFi Connected!");

  pinMode(pinButtonA, INPUT_PULLUP);
  pinMode(pinButtonB, INPUT_PULLUP);
  pinMode(pinButtonC, INPUT_PULLUP);

  pinMode(pinLedA, OUTPUT);
  pinMode(pinLedB, OUTPUT);
  pinMode(pinLedC, OUTPUT);
}

void loop() {
  if (digitalRead(pinButtonA) == LOW) {
    Serial.println("Mood 3");
    digitalWrite(pinLedA, HIGH);
    delay(800);
    digitalWrite(pinLedA, LOW);
    sendMood(3);
    delay(3000);
  }
  else if (digitalRead(pinButtonB) == LOW) {
    Serial.println("Mood 2");
    digitalWrite(pinLedB, HIGH);
    delay(800);
    digitalWrite(pinLedB, LOW);
    sendMood(2);
    delay(3000);
  }
  else if (digitalRead(pinButtonC) == LOW) {
    Serial.println("Mood 1");
    digitalWrite(pinLedC, HIGH);
    delay(800);
    digitalWrite(pinLedC, LOW);
    sendMood(1);
    delay(3000);
  }
}
